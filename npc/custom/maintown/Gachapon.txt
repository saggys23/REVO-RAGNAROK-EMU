// Gachapon
//============================================================
payo2,74,120,6	script	Gachapon	563,{
	if (checkweight(2630,1) == 0) {
		mes "- Sepertinya inventory anda penuh!! -";
		close;
	}
	mes .npc_name$;
	mes "Hello, do you want to play ?";
	next;
	if( select( "Play !", "More Informations", "Leave" ) == 3 ) {
		mes .npc_name$;
		mes "Bye!~";
		close;
	}
	else if( @menu == 2 ) {
		while( getd(".p"+ .@j ) ) {
			mes .npc_name$;
			mes "Item required: ^FFCC00"+ getitemname( getd(".p"+ .@j ) ) +"^000000";
			mes "Chance to gain something: ^CC0000"+ getd(".p"+ .@j +"[1]" ) +"%^000000";
			mes "Possible gains:";
			for( .@i = 3; .@i < getarraysize( getd(".p"+ .@j ) ); .@i +=  3 )
				mes "^0000FFx"+ getd(".p"+ .@j +"["+ .@i +"]" ) +"^000000 "+ getitemname( getd(".p"+ .@j +"["+ (.@i -1) +"]" ) ) +" (^CC0000"+ getd(".p"+ .@j +"["+ (.@i +1) +"]" ) +"%^000000)";
			.@j++;
			next;
		}
	}
	mes .npc_name$;
	mes "Which item do you want to use ?";
	next;
	for( .@j = 0; getd(".p"+ .@j ); .@j++ )
		if( countitem( getd(".p"+ .@j ) ) ) {
			.@size = getarraysize( .@menu$ );
			.@menu$[ .@size ] = getitemname( getd(".p"+ .@j ) );
			.@sel[ .@size ] = .@j;
		}
	.@s = .@sel[ select( implode( .@menu$, ":" ) ) -1 ];
	mes .npc_name$;
	while (1) {
specialeffect 602,"Gachapon";
specialeffect 380,"Gachapon";
sleep2 700;
		mes "Here we go...";
		delitem getd(".p"+ .@s ), 1;
		if( rand(100) > getd(".p"+ .@s +"[1]" ) )// lose
			mes "You got nothing";
		else {
			.@rand = rand( getd(".totalchance"+ .@s ) );
			.@r = 1;
			while ( ( .@rand = .@rand - getd( ".p"+ .@s +"["+ (1+ 3 * .@r) +"]" ) ) >= 0 ) .@r++;
			getitem getd( ".p"+ .@s +"["+ (1+ 3*.@r -2) +"]" ), getd( ".p"+ .@s +"["+( 1+3*.@r -1 )+"]" );
			mes "You got ^FF00CC"+ getd( ".p"+ .@s +"["+( 1+3*.@r -1 )+"]" ) +" "+ getitemname( getd( ".p"+ .@s +"["+ (1+ 3*.@r -2) +"]" ) ) +"^000000";
		}
		mes " ";
		mes "wanna try again ?";
		next;

		if ( select( "Yes", "No" ) == 2 ) close;
		mes .npc_name$;
		if ( countitem( getd(".p"+ .@s ) ) < 1 ) {
			mes "it seems you have ran out of "+ getitemname( getd(".p"+ .@s ) );
			close;
		}
	}
	close;

OnInit:
// (item ID need) (chance), (reward 1) (number of reward 1) (chance to gain), (reward 2) (number of reward 2) (chance to gain)...
	setarray .p0, 671,50,	2462,1,20,	1161,1,30,	5394,1,50;			// gold coin 
	setarray .p1, 7227,20,	2462,1,10,	2541,1,10,	1161,1,10,	13517,1,70;

	while ( getd(".p"+ .@j ) ) {
		for( .@i = 4; .@i < getarraysize( getd(".p"+ .@j ) ); .@i += 3 )
			setd ".totalchance"+ .@j, getd(".totalchance"+ .@j ) + getd(".p"+ .@j +"["+ .@i +"]" );
		.@j++;
	}
	
	.npc_name$ = "[ "+ strnpcinfo(1) +"]";
	end;
}